{ src, stdenv, fetchurl, fetchzip, meson, ninja, pkgconfig, elixir, glib }:

with stdenv.lib;

let
  gzseek = stdenv.mkDerivation {
    name = "gzseek";

    src = fetchzip {
      url = "https://github.com/serokell/gzseek/archive/742ea67e84c8dfffcca3b239320cc9f321487d75.tar.gz";
      sha256 = "0pi871r60kh5xjlkz8paxsasqqjbgdddk8g4hmp4cp32cbx69xlw";
    };

    nativeBuildInputs = [ meson ninja pkgconfig ];
    buildInputs = [ glib ];
  };

  hex = fetchurl {
    url = "https://repo.hex.pm/installs/1.6.0/hex-0.18.1.ez";
    sha512 = "9c806664a3341930df4528be38b0da216a31994830c4437246555fe3027c047e073415bcb1b6557a28549e12b0c986142f5e3485825a66033f67302e87520119";
  };

  fetchHex = { pname, version, sha256 }: stdenv.mkDerivation {
    name = "hex-${pname}-${version}";

    src = fetchurl {
      url = "https://repo.hex.pm/tarballs/${pname}-${version}.tar";
      inherit sha256;

      postFetch = ''
        tar -xf $downloadedFile
        cat VERSION metadata.config contents.tar.gz > $out
      '';
    };

    buildCommand = ''
      mkdir $out && cd $out
      ${gzseek}/bin/gzseek $src | tar -zxf -
      echo ${pname},${version},${sha256},hexpm > .hex
    '';
  };

  closure = {
<%= closure %>
  };

  linkDependency = name: src: "ln -s ${src} deps/${name}";
in

stdenv.mkDerivation {
  name = "mix-project";
  inherit src;

  buildInputs = [ elixir ];

  configurePhase = ''
    runHook preConfigure

    mkdir deps
    ${concatStringsSep "\n" (mapAttrsToList linkDependency closure)}

    runHook postConfigure
  '';

  HOME = ".";
  MIX_ENV = "prod";

  buildPhase = ''
    runHook preBuild

    mix archive.install --force ${hex}
    mix compile

    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir $out
    mv * $out

    runHook postInstall
  '';
}
