root: { stdenv, fetchurl, fetchzip, meson, ninja, pkgconfig, elixir, glib, rebar, rebar3 }:

with stdenv.lib;

let
  gzseek = stdenv.mkDerivation {
    name = "gzseek";

    src = fetchzip {
      url = "https://github.com/serokell/gzseek/archive/742ea67e84c8dfffcca3b239320cc9f321487d75.tar.gz";
      sha256 = "0pi871r60kh5xjlkz8paxsasqqjbgdddk8g4hmp4cp32cbx69xlw";
    };

    nativeBuildInputs = [ meson ninja pkgconfig ];
    buildInputs = [ glib ];
  };

  hex = fetchurl {
    url = "https://repo.hex.pm/installs/1.6.0/hex-0.18.1.ez";
    sha512 = "9c806664a3341930df4528be38b0da216a31994830c4437246555fe3027c047e073415bcb1b6557a28549e12b0c986142f5e3485825a66033f67302e87520119";
  };

  fetchHex = { pname, version, sha256 }: stdenv.mkDerivation {
    name = "hex-${pname}-${version}";

    src = fetchurl {
      url = "https://repo.hex.pm/tarballs/${pname}-${version}.tar";
      inherit sha256;

      postFetch = ''
        tar -xf $downloadedFile
        cat VERSION metadata.config contents.tar.gz > $out
      '';
    };

    buildCommand = ''
      mkdir $out && cd $out
      ${gzseek}/bin/gzseek $src | tar -zxf -
      echo ${pname},${version},${sha256},hexpm > .hex
    '';
  };

  closure = {
<%= closure %>
  };

  # Deep link deps so mix can write build files in the dep dirs, then fool mix
  # to think any git deps are checked out.
  linkDependency = name: src: ''
    cp -rs --no-preserve=mode ${src} deps/${name}
    mkdir deps/${name}/.git
    touch deps/${name}/.git/HEAD
  '';
in

stdenv.mkDerivation {
  name = "mix-project";
  src = root;

  buildInputs = [ elixir rebar rebar3 ];

  configurePhase = ''
    runHook preConfigure

    mkdir deps
    ${concatStringsSep "\n" (mapAttrsToList linkDependency closure)}

    runHook postConfigure
  '';

  HOME = ".";

  MIX_ENV = "prod";
  MIX_REBAR = "${rebar}/bin/rebar";
  MIX_REBAR3 = "${rebar3}/bin/rebar3";

  buildPhase = ''
    runHook preBuild

    mix archive.install --force ${hex}
    mix deps.compile --force
    mix compile --no-deps-check

    runHook postBuild
  '';

  installPhase = ''
    runHook preInstall

    mkdir $out
    mv * $out

    runHook postInstall
  '';
}
